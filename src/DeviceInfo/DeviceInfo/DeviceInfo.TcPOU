<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="DeviceInfo" Id="{bbbb3437-c728-49f1-b8eb-fac3c161592d}" SpecialFunc="None">
    <Declaration><![CDATA[/// Testdoc
{attribute 'no_explicit_call'}
{attribute 'conditionalshow_all_locals'}
FUNCTION_BLOCK DeviceInfo EXTENDS ZCore.Object IMPLEMENTS ZCore.IUnmanagedObject
VAR_OUTPUT
  NameArray : ARRAY[0..Parameter.MaxDeviceToBeFound] OF ZCore.ZString;
  DeviceIdArray : ARRAY[0..Parameter.MaxDeviceToBeFound] OF UDINT;
  NetIdArray : ARRAY[0..Parameter.MaxDeviceToBeFound] OF Tc2_System.T_AmsNetID;
END_VAR
VAR
  _amsNetId : Tc2_System.T_AmsNetID;
  _ethercatMasterCount : UINT;
  _step : ZCore.Step(DeviceInfoSteps.Begin, DeviceInfoSteps.End);
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Cyclic" Id="{2267b843-f919-45ab-a87e-d7470b57bd04}">
      <Declaration><![CDATA[/// The cyclic method should be run in any PLC-cycle. If the object has no work to do, we will simply
/// RETURN here. If an action has been started, this cyclic method takes care of all the work to do.
METHOD Cyclic
VAR_INST
  itIndex : UINT;
  ethercatMasters : UINT;

  getDeviceType : Tc2_IoFunctions.IOF_GetDeviceType;
  getDeviceName : Tc2_IoFunctions.IOF_GetDeviceName;
  getDeviceNetIds : Tc2_IoFunctions.IOF_GetDeviceNetId;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE _step.Index OF
  DeviceInfoSteps.Begin:
    IF Busy
    THEN
      ResetMem();
      _ethercatMasterCount := 0;
      _step.SetNext(DeviceInfoSteps.FetchDeviceTypes);
    END_IF
  
  DeviceInfoSteps.FetchDeviceTypes:
    IF _step.OnEntry()
    THEN
      getDeviceType(START:=FALSE);
      getDeviceType(START:=TRUE, NETID:=_amsNetId, DEVICEID:=ItIndex, TMOUT:=T#2S);
    END_IF
    
    getDeviceType(START:=FALSE);
    IF getDeviceType.ERR // no valid device found
    THEN
      IF ItIndex > Parameter.MaxDevicesSearchCount 
      THEN
        _step.SetNext(DeviceInfoSteps.ResetId1);
      ELSE
        _step.SetNext(DeviceInfoSteps.FetchDeviceTypes);
      END_IF
      
      itIndex := itIndex + 1;
    ELSIF NOT getDeviceType.BUSY
    THEN
      IF getDeviceType.IODeviceType = Tc2_IoFunctions.IODEVICETYPE_ETHERCAT
      THEN
        DeviceIdArray[_ethercatMasterCount] := itIndex;
        _ethercatMasterCount := _ethercatMasterCount + 1;
        itIndex := itIndex + 1;
        _step.SetNext(DeviceInfoSteps.ResetId1);
      END_IF
    END_IF

  DeviceInfoSteps.ResetId1:
    itIndex := 0;
    _step.SetNext(DeviceInfoSteps.GetDeviceNames);  
    
  DeviceInfoSteps.GetDeviceNames:
    IF _step.OnEntry()
    THEN
      getDeviceName(START:=FALSE);
      getDeviceName(START:=TRUE, NETID:=_amsNetId, DEVICEID:=DeviceIdArray[ItIndex], TMOUT:=T#2S);
    END_IF  
    
    getDeviceName();
    IF getDeviceName.ERR
    THEN
      Abort('Name could not be fetched!');
    ELSIF NOT getDeviceName.BUSY
    THEN
      NameArray[itIndex] := getDeviceName.DEVICENAME;
      itIndex := itIndex + 1;
      IF DeviceIdArray[itIndex] <> 0
      THEN
        _step.SetNext(DeviceInfoSteps.GetDeviceNames);
      ELSE
        _step.SetNext(DeviceInfoSteps.ResetId2);
      END_IF
    END_IF
    
  DeviceInfoSteps.ResetId2:
    itIndex := 0;
    _step.SetNext(DeviceInfoSteps.GetDeviceNetIds);  
    
  DeviceInfoSteps.GetDeviceNetIds:
    IF _step.OnEntry()
    THEN
      getDeviceNetIds(START:=FALSE);
      getDeviceNetIds(START:=TRUE, NETID:=_amsNetId, DEVICEID:=DeviceIdArray[itIndex], TMOUT:=T#2S);
    END_IF  
    
    getDeviceNetIds();
    IF getDeviceNetIds.ERR
    THEN
      Abort('Name could not be fetched!');
    ELSIF NOT getDeviceNetIds.BUSY
    THEN
      NetIdArray[itIndex] := getDeviceNetIds.DeviceNetId;
      itIndex := itIndex + 1;
      IF DeviceIdArray[itIndex] = 0
      THEN
        SetBusy(FALSE);
        _step.SetNext(DeviceInfoSteps.End);
      END_IF
    END_IF      
  
  DeviceInfoSteps.End:
    RETURN;
    
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Property Name="EthercatMasterCount" Id="{1d505375-1582-4f78-a165-b684fe281a6d}">
      <Declaration><![CDATA[/// Returns the actual found Ethercat Master count -> Needed to iterate through the VAR_INPUT Arrays of AMSNet-IDs and
/// Ethercat Master Device names.
PROPERTY EthercatMasterCount : UINT]]></Declaration>
      <Get Name="Get" Id="{de855259-9a12-43d0-803f-73205d170aaf}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[EthercatMasterCount := _ethercatMasterCount;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="FB_init" Id="{755219fb-0948-4ab3-9f08-dc3591e71935}">
      <Declaration><![CDATA[/// Sets the object to Idle-State becuse no booting phase is needed here!
METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SetIdle();]]></ST>
      </Implementation>
    </Method>
    <Method Name="ReadDeviceNamesAsync" Id="{13141ac0-b5ae-431f-95b4-bf53b0233b7e}">
      <Declaration><![CDATA[/// Starts reading of all available Ethercat-Master Device names
/// Just wait until Busy property of this object returns FALSE -> then fetch all Ethercat Master names from
/// the Array in VAR_OUTPUT of this object
METHOD ReadDeviceNamesAsync
VAR_INPUT
  amsNetId : Tc2_System.T_AmsNetID; //< AMS-Netid of the IPC on which the device names should be fetched
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[SetBusy(TRUE);
_amsNetId := amsNetId;
_step.SetNext(DeviceInfoSteps.Begin);]]></ST>
      </Implementation>
    </Method>
    <Method Name="ResetMem" Id="{ba2b5e99-106e-4ec9-9a66-90cf17d95491}">
      <Declaration><![CDATA[METHOD PRIVATE ResetMem
VAR
  count : UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR count:=0 TO Parameter.MaxDeviceToBeFound BY 1 DO
  NameArray[count] := '';
  DeviceIdArray[count] := 0;
  NetIdArray[count] := '';
END_FOR]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>